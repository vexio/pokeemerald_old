raw `
Route106_MapScripts:: @ 81EE489
	.byte 0

Route106_EventScript_TrainerTipsSign:: @ 81EE48A
	msgbox Route106_Text_TrainerTips, MSGBOX_SIGN
	end

Route106_EventScript_Douglas:: @ 81EE493
	trainerbattle_single TRAINER_DOUGLAS, Route106_Text_DouglasIntro, Route106_Text_DouglasDefeated
	msgbox Route106_Text_DouglasPostBattle, MSGBOX_AUTOCLOSE
	end

Route106_EventScript_Kyla:: @ 81EE4AA
	trainerbattle_single TRAINER_KYLA, Route106_Text_KylaIntro, Route106_Text_KylaDefeated
	msgbox Route106_Text_KylaPostBattle, MSGBOX_AUTOCLOSE
	end

Route106_EventScript_Elliot:: @ 81EE4C1
	trainerbattle_single TRAINER_ELLIOT_1, Route106_Text_ElliotIntro, Route106_Text_ElliotDefeated, Route106_EventScript_ElliotRegisterMatchCallAfterBattle
	specialvar VAR_RESULT, ShouldTryRematchBattle
	compare VAR_RESULT, TRUE
	goto_if_eq Route106_EventScript_ElliotRematch
	msgbox Route106_Text_ElliotPostBattle, MSGBOX_DEFAULT
	release
	end

Route106_EventScript_ElliotRegisterMatchCallAfterBattle:: @ 81EE4ED
	special PlayerFaceTrainerAfterBattle
	waitmovement 0
	msgbox Route106_Text_ElliotRegister, MSGBOX_DEFAULT
	register_matchcall TRAINER_ELLIOT_1
	release
	end

Route106_EventScript_ElliotRematch:: @ 81EE50C
	trainerbattle_rematch TRAINER_ELLIOT_1, Route106_Text_ElliotRematchIntro, Route106_Text_ElliotRematchDefeated
	msgbox Route106_Text_ElliotRematchPostBattle, MSGBOX_AUTOCLOSE
	end

Route106_EventScript_Ned:: @ 81EE523
	trainerbattle_single TRAINER_NED, Route106_Text_NedIntro, Route106_Text_NedDefeated
	msgbox Route106_Text_NedPostBattle, MSGBOX_AUTOCLOSE
	end

Route106_Text_TrainerTips: @ 81EE53A
	.string "TRAINER TIPS\p"
	.string "Advice on catching POKéMON with a ROD:\n"
	.string "Press the A Button if you get a bite.$"


`

const PLAYER = OBJ_EVENT_ID_PLAYER
const GRUNT_1 = 5 // male
const GRUNT_2 = 4 // male w/ accent
const GRUNT_3 = 3 // female
const HAUMEA = 2
const CAMERA = OBJ_EVENT_ID_CAMERA
const SKIP = 15

script TriadGorge_Trigger_ProtoGrunts {
    lockall
    fadescreen(FADE_TO_BLACK)
    cleartrainerflag(TRAINER_GRUNT_TRIAD_GORGE_1)
    cleartrainerflag(TRAINER_GRUNT_TRIAD_GORGE_2)
    cleartrainerflag(TRAINER_GRUNT_TRIAD_GORGE_3)
    cleartrainerflag(TRAINER_ADMIN_HAUMEA_1)
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if (var(VAR_TEMP_1) == 20) {
        applymovement(PLAYER, TriadGorge_ProtoGrunts_Movement_0_0)
        waitmovement(0)
    } elif(var(VAR_TEMP_1) == 22) {
		applymovement(PLAYER, TriadGorge_ProtoGrunts_Movement_0_1)
        waitmovement(0)
	} elif(var(VAR_TEMP_1) == 23) {
		applymovement(PLAYER, TriadGorge_ProtoGrunts_Movement_0_2)
        waitmovement(0)
	}
	delay(5)
	special(SpawnCameraObject)
	applymovement(CAMERA, TriadGorge_ProtoGrunts_CMovement_0)
	waitmovement(0)
	playbgm(MUS_AJITO, 0)
	fadescreen(FADE_FROM_BLACK)
	delay(10)
	applymovement(HAUMEA, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	msgbox(format("Haumea: {COLOR BLUE}You three!\nWhere have you been?"))
	applymovement(GRUNT_3, Common_Movement_WalkInPlaceRight)
	waitmovement(0)
	msgbox(format("Grunt 3: {COLOR RED}Sorry {COLOR GREEN}Haumea{COLOR RED}, won't happen again!"))
	applymovement(GRUNT_3, Common_Movement_WalkInPlaceUp)
	waitmovement(0)
	msgbox(format("{COLOR RED}Right boys?"), MSGBOX_AUTOCLOSE)
	delay(10)
	applymovement(GRUNT_1, Common_Movement_JumpInPlaceRight)
	applymovement(GRUNT_2, Common_Movement_JumpInPlaceRight)
	playse(SE_JITE_PYOKO)
	waitmovement(0)
	applymovement(GRUNT_1, Common_Movement_WalkInPlaceRight)
	applymovement(GRUNT_2, Common_Movement_WalkInPlaceRight)
	waitmovement(0)
	applymovement(GRUNT_1, Common_Movement_ExclamationMark)
	applymovement(GRUNT_2, Common_Movement_ExclamationMark)
	waitmovement(0)
	playse(SE_PIN)
	delay(16)
	msgbox(format("Grunt 1: {COLOR BLUE}Yes {COLOR GREEN}Haumea{COLOR BLUE} sir!\n{COLOR DARK_GREY}Grunt 2: {COLOR BLUE}Yes {COLOR GREEN}Haumea{COLOR BLUE} sir!"), MSGBOX_AUTOCLOSE)
	delay(10)
	applymovement(GRUNT_3, Common_Movement_WalkInPlaceRight)
	applymovement(HAUMEA, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	msgbox(format("Haumea: {COLOR BLUE}Good.\pNow, do you guys have those dusty old books?"), MSGBOX_AUTOCLOSE)
	delay(5)
	applymovement(GRUNT_2, Common_Movement_QuestionMark)
	waitmovement(0)
	playse(SE_PIN)
	delay(16)
	msgbox(format("Grunt 2: {COLOR BLUE}Uhhh...\nBooks? As in multipl'?"))
	applymovement(HAUMEA, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	msgbox(format("Haumea: {COLOR BLUE}Yes as in multiple!\pWe need the {COLOR GREEN}Dorimtome{COLOR BLUE} and the {COLOR GREEN}Akumutome{COLOR BLUE}!"))
	applymovement(GRUNT_2, Common_Movement_WalkInPlaceRight)
	waitmovement(0)
	msgbox(format("Grunt 2: {COLOR BLUE}...\nWhic' one do I hav' here?"))
	applymovement(GRUNT_3, Common_Movement_WalkInPlaceUp)
	waitmovement(0)
	msgbox(format("Grunt 3: {COLOR RED}Let me see that!"), MSGBOX_AUTOCLOSE)
	applymovement(GRUNT_2, Common_Movement_WalkInPlaceDown)
	waitmovement(0)
	delay(5)
	applymovement(GRUNT_3, Common_Movement_WalkInPlaceUp)
	waitmovement(0)
	delay(20)
	msgbox(format("Grunt 3: {COLOR RED}...\pIt looks like it's the {COLOR GREEN}Akumutome{COLOR BLUE}."))
	msgbox(format("Grunt 2: {COLOR BLUE}That's th' one I got from {COLOR GREEN}Alnerwick Library{COLOR BLUE}."))
	applymovement(GRUNT_2, Common_Movement_WalkInPlaceRight)
	applymovement(GRUNT_3, Common_Movement_WalkInPlaceRight)
	waitmovement(0)
	msgbox(format("Haumea: {COLOR BLUE}Did anyone see you steal that book?\p{COLOR DARK_GREY}Grunt 2: {COLOR BLUE}Only the librarian and some kid.\p{COLOR DARK_GREY}Haumea: {COLOR BLUE}Good.\nNow you three need to go get that {COLOR GREEN}Dorimtome{COLOR BLUE} in {COLOR GREEN}Enclava Tower{COLOR BLUE}!"))
	applymovement(GRUNT_1, Common_Movement_WalkInPlaceRight)
	applymovement(GRUNT_2, Common_Movement_WalkInPlaceRight)
	applymovement(GRUNT_3, Common_Movement_WalkInPlaceRight)
	waitmovement(0)
	msgbox(format("Grunts: {COLOR BLUE}Yes sir!"), MSGBOX_AUTOCLOSE)
	delay(15)
	applymovement(GRUNT_1, Common_Movement_WalkInPlaceLeft)
	applymovement(GRUNT_2, Common_Movement_WalkInPlaceLeft)
	applymovement(GRUNT_3, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	delay(10)
	applymovement(GRUNT_1, Common_Movement_ExclamationMark)
	applymovement(GRUNT_2, Common_Movement_ExclamationMark)
	applymovement(GRUNT_3, Common_Movement_ExclamationMark)
	waitmovement(0)
	playse(SE_PIN)
	delay(16)
	applymovement(GRUNT_2, Common_Movement_JumpInPlaceLeft)
	waitmovement(0)
	playse(SE_JITE_PYOKO)
	delay(5)
	msgbox(format("Grunt 2: {COLOR BLUE}That's th' kid from {COLOR GREEN}Alnerwick Library{COLOR BLUE}!"))
	applymovement(HAUMEA, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	msgbox(format("Haumea: {COLOR BLUE}So you've been eaves dropping, eh kid?\pGrunts!\nTake care of that nuisance!\pI don't plan on getting my hands dirty."), MSGBOX_AUTOCLOSE)
	delay(10)
	applymovement(CAMERA, TriadGorge_ProtoGrunts_CMovement_1)
	waitmovement(0)
	special(RemoveCameraObject)
	delay(10)
	applymovement(GRUNT_1, TriadGorge_ProtoGrunts_Movement_1)
	waitmovement(0)
	applymovement(PLAYER, Common_Movement_WalkInPlaceUp)
	waitmovement(0)
	msgbox(format("Grunt 1: {COLOR BLUE}You're gonna regret messing with {COLOR GREEN}Team Proto{COLOR BLUE}!"))
	trainerbattle_no_intro(TRAINER_GRUNT_TRIAD_GORGE_1, format("Gah!"))
	playbgm(MUS_AJITO, 0)
	delay(5)
	applymovement(GRUNT_2, TriadGorge_ProtoGrunts_Movement_2)
	waitmovement(0)
	applymovement(PLAYER, Common_Movement_WalkInPlaceRight)
	waitmovement(0)
	msgbox(format("Grunt 2: {COLOR BLUE}You're th' kid from {COLOR GREEN}Alnerwick Library{COLOR BLUE}eh?\pStay outta our busines'!"))
	trainerbattle_no_intro(TRAINER_GRUNT_TRIAD_GORGE_2, format("Stay outta it kid!"))
	playbgm(MUS_AJITO, 0)
	delay(5)
	applymovement(GRUNT_3, TriadGorge_ProtoGrunts_Movement_3)
	waitmovement(0)
	applymovement(PLAYER, Common_Movement_WalkInPlaceDown)
	waitmovement(0)
	msgbox(format("Grunt 3: {COLOR RED}Those two bafoons don't know how to represent {COLOR GREEN}Team Proto{COLOR RED}!"))
	trainerbattle_no_intro(TRAINER_GRUNT_TRIAD_GORGE_3, format("Running out of energy?"))
	playbgm(MUS_AJITO, 0)
	delay(5)
	applymovement(GRUNT_2, TriadGorge_ProtoGrunts_Movement_4)
	waitmovement(0)
	applymovement(PLAYER, Common_Movement_WalkInPlaceRight)
	applymovement(HAUMEA, TriadGorge_ProtoGrunts_Movement_5)
	waitmovement(0)
	msgbox(format("Haumea: {COLOR BLUE}These idiots don't know how to do anything right!\pListen, kid.\nI know you're trying to be the hero, and trying to get that book back to {COLOR GREEN}Alnerwick Library{COLOR BLUE}, but that ideology won't work here.\pNow, time to teach you a valuable lesson here, kid!"))
	trainerbattle_no_intro(TRAINER_ADMIN_HAUMEA_1, format("What?\pA kid beat me?"))
	playbgm(MUS_AJITO, 0)
	delay(5)
	applymovement(HAUMEA, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	msgbox(format("Haumea: {COLOR BLUE}You're a strong trainer, I can see that...\pBut {COLOR GREEN}Team Proto{COLOR BLUE} won't be stopped by a child!\pThis won't be the last you see of {COLOR GREEN}Team Proto{COLOR BLUE}!\pAbra, use Teleport!"))
	delay(10)
	playmoncry(SPECIES_ABRA, 0)
	waitmoncry
	delay(10)
	fadescreen(FADE_TO_BLACK)
	closemessage
	setvar(VAR_PROTOGRUNTS_ENCLAVAGORGE, 3)
	removeobject(GRUNT_1)
	removeobject(GRUNT_2)
	removeobject(GRUNT_3)
	removeobject(HAUMEA)
	playse(SE_TK_WARPOUT)
	setobjectxy(SKIP, 36, 21)
	delay(15)
	fadescreen(FADE_FROM_BLACK)
	delay(20)
	applymovement(SKIP, TriadGorge_ProtoGrunts_Movement_6)
	waitmovement(0)
	applymovement(SKIP, Common_Movement_ExclamationMark)
	waitmovement(0)
	playse(SE_PIN)
	waitse
	playbgm(MUS_RG_RIVAL1, 0)
	msgbox(format("Skip: {COLOR BLUE}There you are slowpoke!"), MSGBOX_AUTOCLOSE)
	applymovement(SKIP, TriadGorge_ProtoGrunts_Movement_7)
	waitmovement(0)
	msgbox(format("Skip: {COLOR BLUE}Did you just deal with those robed people?\pThey looked weak, I'm surprised you stood a chance against them!\pI've been training for the {COLOR GREEN}Fiery Badge{COLOR BLUE} up north here on {COLOR GREEN}Frostpine Climb{COLOR BLUE}, and now my team is stronger than ever!\pI'm not gonna take it out on you, since you and your team just took a beating from those {COLOR GREEN}Team Proto{COLOR BLUE} goons.\pI also found this HM, I don't think I'm going to have any use for it."))
	giveitem(ITEM_HM01_CUT, 1)
	msgbox(format("Skip: {COLOR BLUE}Anyways, I'm gonna head up north to get the third badge!\pI'm going to continue training until I become the strongest trainer in all of Ryoko!"), MSGBOX_AUTOCLOSE)
	applymovement(SKIP, TriadGorge_ProtoGrunts_Movement_8)
	playbgm(MUS_NEXTROAD, 0)
	waitmovement(0)
	removeobject(SKIP)
	setflag(FLAG_TRIAD_GORGE_GRUNTS)
	release
	end
}
movement TriadGorge_ProtoGrunts_Movement_0_0 {
    walk_fastest_down
    walk_in_place_fastest_right
}
movement TriadGorge_ProtoGrunts_Movement_0_1 {
    walk_fastest_up
    walk_in_place_fastest_right
}
movement TriadGorge_ProtoGrunts_Movement_0_2 {
    walk_fastest_up * 2
    walk_in_place_fastest_right
}
movement TriadGorge_ProtoGrunts_CMovement_0 {
	walk_fastest_right * 5
}
movement TriadGorge_ProtoGrunts_CMovement_1 {
	walk_left * 5
}
movement TriadGorge_ProtoGrunts_Movement_1 {
    walk_left * 2
    walk_in_place_down
}
movement TriadGorge_ProtoGrunts_Movement_2 {
    walk_left
}
movement TriadGorge_ProtoGrunts_Movement_3 {
    walk_left * 2
	walk_in_place_up
}
movement TriadGorge_ProtoGrunts_Movement_4 {
    walk_up
	walk_in_place_down
}
movement TriadGorge_ProtoGrunts_Movement_5 {
    walk_left * 4
}

movement TriadGorge_ProtoGrunts_Movement_6 {
	walk_left * 4
}
movement TriadGorge_ProtoGrunts_Movement_7 {
	walk_left * 3
}
movement TriadGorge_ProtoGrunts_Movement_8 {
	walk_right * 8
}

//trainers

script TriadGorge_Trainer_BirdKeeper {
	trainerbattle_single(TRAINER_BIRDKEEP_KEITH, format("{COLOR BLUE}I've been trying to train my Pokémon, and you look like just enough experience!"), format("Back to battling Wild Pokémon..."))
	msgbox(format("{COLOR BLUE}I've been training my Pokémon by battling Wild Pokémon, but battling trainers seems to give me more experience."), MSGBOX_NPC)
	end
}

script TriadGorge_Trainer_BugCatcher {
	trainerbattle_single(TRAINER_BUGCATCHER_DEVEN, format("{COLOR BLUE}Buzz, buzz!"), format("Buzz, buzz..."))
	msgbox(format("{COLOR BLUE}Buzz, buzz!\pI like to hunt for Combee's!"), MSGBOX_NPC)
	end
}
script TriadGorge_Trainer_RuinManiac {
	trainerbattle_single(TRAINER_RUINMANIAC_WAYNE, format("{COLOR BLUE}Hoho!\pDo you have any Rock-type Pokémon?"), format("Hoho!\nGood battle champ!"))
	msgbox(format("{COLOR BLUE}Rock-type Pokémon are the best, but the best Rock-type Pokémon are Fossil Pokémon!"), MSGBOX_NPC)
	end
}
script TriadGorge_Trainer_PokefanF {
	trainerbattle_single(TRAINER_POKEFAN_F_ALENA, format("{COLOR RED}What Pokémon do you have?\pLet me see!"), format("Wow!\nYou have some cool Pokémon!"))
	msgbox(format("{COLOR RED}If you ever find any cool Pokémon, come show me!"), MSGBOX_NPC)
	end
}

script TriadGorge_Trainer_Hiker {
	trainerbattle_single(TRAINER_HIKER_DAMON, format("{COLOR BLUE}Whew!\pI just got done with my hike down {COLOR GREEN}Frostpine Clime{COLOR BLUE}!\pHow about a little battle, eh?"), format("A great way to wind down from a hike."))
	msgbox(format("{COLOR BLUE}A Pokémon battle is always a great way to wind down!"), MSGBOX_NPC)
	end
}
script TriadGorge_Sign_RouteSignLeft {
	msgbox(format("{COLOR LIGHT_GREEN}Triad Gorge\p{COLOR DARK_GREY}{UP_ARROW} {COLOR LIGHT_GREEN}Enclava Town\n{COLOR DARK_GREY}{RIGHT_ARROW}{UP_ARROW} {COLOR LIGHT_GREEN}Frostpine Climb\l{COLOR DARK_GREY}{RIGHT_ARROW} {COLOR LIGHT_GREEN}Myst City"), MSGBOX_SIGN, MSGBOX_SUB_WOOD)
}
script TriadGorge_Sign_RouteSignRight {
	msgbox(format("{COLOR LIGHT_GREEN}Triad Gorge\p{COLOR DARK_GREY}{RIGHT_ARROW} {COLOR LIGHT_GREEN}Myst City\n{COLOR DARK_GREY}{LEFT_ARROW}{UP_ARROW} {COLOR LIGHT_GREEN}Frostpine Climb\l{COLOR DARK_GREY}{LEFT_ARROW} {COLOR LIGHT_GREEN}Enclava Town"), MSGBOX_SIGN, MSGBOX_SUB_WOOD)
}
script TriadGorge_Sign_RouteSignMiddle {
	msgbox(format("{COLOR LIGHT_GREEN}Triad Gorge\p{COLOR DARK_GREY}{UP_ARROW} {COLOR LIGHT_GREEN}Frostpine Climb\n{COLOR DARK_GREY}{LEFT_ARROW} {COLOR LIGHT_GREEN}Enclava Town\l{COLOR DARK_GREY}{RIGHT_ARROW} {COLOR LIGHT_GREEN}Myst City"), MSGBOX_SIGN, MSGBOX_SUB_WOOD)
}

script TriadGorge_Object_RockSmashHiker {
	lock
	faceplayer
	msgbox(format("{COLOR BLUE}These rocks are lookin' like they're blocking the way to {COLOR GREEN}Myst City{COLOR BLUE}.\pIf I had the {COLOR GREEN}Fiery Badge{COLOR BLUE} I could smash these rocks!"))
	if (flag(FLAG_BADGE03_GET) == true) {
		applymovement(14, Common_Movement_ExclamationMark)
		waitmovement(0)
		playse(SE_PIN)
		waitse
		msgbox(format("{COLOR BLUE}Is that the {COLOR GREEN}Fiery Badge{COLOR BLUE}?\pWowza!\nYou must be a strong trainer!\pHere, take this!"))
		giveitem(ITEM_HM06_ROCK_SMASH, 1)
		msgbox(format("{COLOR BLUE}With that HM, you can remove these rocks and similar looking ones without a problem!\pI'm going to go get myself the {COLOR GREEN}Fiery Badge{COLOR BLUE} as well so I can use Rock Smash too!"), MSGBOX_AUTOCLOSE)
		delay(10)
		getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
		if (var(VAR_TEMP_0) == 66) {
			applymovement(14, TriadGorge_RockSmashHiker_Movement_1)
			waitmovement(0)
		} else {
			applymovement(14, TriadGorge_RockSmashHiker_Movement_0)
			waitmovement(0)
		}
		removeobject(14)
		setflag(FLAG_TRIADGORGE_ROCKSMASH)
		release
		end
	} else {
		closemessage
		release
		end
	}
}

movement TriadGorge_RockSmashHiker_Movement_0 {
	walk_left * 7
	walk_up * 7
}
movement TriadGorge_RockSmashHiker_Movement_1 {
	walk_up
	walk_left * 7
	walk_up * 6
}